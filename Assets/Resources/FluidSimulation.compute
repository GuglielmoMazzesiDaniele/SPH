// Each #kernel tells which function to compile; you can have many kernels
#pragma kernel update_predicted_positions
#pragma kernel update_spatial_keys
#pragma kernel reorder_buffers
#pragma kernel copy_auxiliary_in_main
#pragma kernel update_density
#pragma kernel update_position_and_velocity
#pragma kernel update_density_map

// CONSTANTS
static const int thread_group_size = 256;

// GPU RELATED DATA

// Buffer containing the data of the simulation
RWStructuredBuffer<float3> positions;
RWStructuredBuffer<float3> predicted_positions;
RWStructuredBuffer<float3> velocities;
RWStructuredBuffer<float> densities;

// Spatial Hashing Grid's buffers
RWStructuredBuffer<uint> spatial_keys;
RWStructuredBuffer<uint> spatial_indices;
RWStructuredBuffer<uint> spatial_offsets;

// Ray-marching rendering texture map
RWTexture3D<float> density_map;

// Auxiliary buffers used during the resorting process
RWStructuredBuffer<float3> auxiliary_positions;
RWStructuredBuffer<float3> auxiliary_predicted_positions;
RWStructuredBuffer<float3> auxiliary_velocities;

// Spatial Hashing 3D offsets
static const int3 cell_offsets_3D[27] =
{
    int3 (-1,  1,  1),
    int3 ( 0,  1,  1),
    int3 ( 1,  1,  1),
    int3 (-1,  0,  1),
    int3 ( 0,  0,  1),
    int3 ( 1,  0,  1),
    int3 (-1, -1,  1),
    int3 ( 0, -1,  1),
    int3 ( 1, -1,  1),
    int3 (-1,  1,  0),
    int3 ( 0,  1,  0),
    int3 ( 1,  1,  0),
    int3 (-1,  0,  0),
    int3 ( 0,  0,  0),
    int3 ( 1,  0,  0),
    int3 (-1, -1,  0),
    int3 ( 0, -1,  0),
    int3 ( 1, -1,  0),
    int3 (-1,  1, -1),
    int3 ( 0,  1, -1),
    int3 ( 1,  1, -1),
    int3 (-1,  0, -1),
    int3 ( 0,  0, -1),
    int3 ( 1,  0, -1),
    int3 (-1, -1, -1),
    int3 ( 0, -1, -1),
    int3 ( 1, -1, -1),
};

// CPU PROVIDED DATA

// Simulation related fields
int particles_amount;
float delta_time;
float gravity;
float3 gravity_acceleration;
float particle_mass;
float rest_density;
float stiffness;
float viscosity;
float collision_damping;
float3 bounds_size;
float3 half_bounds_size;
float4x4 world_to_local;
float4x4 local_to_world;

// Kernel related fields
float kernel_radius;
float sqr_kernel_radius;
float poly6_normalization;
float spiky_gradient_first_term;
float viscosity_laplacian_first_term;

// Ray-marching related fields
const int3 density_map_size;

// Computes the influence at the given squared distance using the kernel: (radius^2 - distance^2)^3
float poly6(float sqr_distance)
{
    // Computing the influence at given distance
    return poly6_normalization * pow(sqr_kernel_radius - sqr_distance, 3); 
}

// Computes the gradient of the Spiky kernel at the given distance
float spiky_gradient(float distance)
{
    return spiky_gradient_first_term * pow(distance - kernel_radius, 2);
}

// Computes the laplacian of the Viscosity kernel at the given distance
float viscosity_laplacian(float distance)
{
    return viscosity_laplacian_first_term * (kernel_radius - distance);
}

// Resolve particles collision with simulation's bounds
void resolve_collision(uint target_particle_index)
{
    // Extracting the current particle position and velocity from the buffers and converting in local space
    float3 local_position = mul(world_to_local, float4(positions[target_particle_index], 1.0f)).xyz;
    float3 local_velocity = mul(world_to_local, float4(velocities[target_particle_index], 0.0f)).xyz;
    
    // Case in which the particle collided with the bounds on X-axis
    if (abs(local_position.x) > half_bounds_size.x)
    {
        // Setting the position x to the maximum
        local_position.x = half_bounds_size.x * sign(local_position.x);
        // Flipping the velocity sign and applying a damping
        local_velocity.x *= -collision_damping;
    }
        
    // Case in which the particle collided with the bounds on Y-axis
    if (abs(local_position.y) > half_bounds_size.y)
    {
        // Setting the position y to the maximum
        local_position.y = half_bounds_size.y * sign(local_position.y);
        // Flipping the velocity sign and applying a damping
        local_velocity.y *= -collision_damping;
    }

    // Case in which the particle collided with the bounds on Z-axis
    if (abs(local_position.z) > half_bounds_size.z)
    {
        // Setting the position y to the maximum
        local_position.z = half_bounds_size.z * sign(local_position.z);
        // Flipping the velocity sign and applying a damping
        local_velocity.z *= -collision_damping;
    }

    // Converting back in world space
    positions[target_particle_index] = mul(local_to_world, float4(local_position, 1.0f)).xyz;
    velocities[target_particle_index] = mul(local_to_world, float4(local_velocity, 1.0f)).xyz;
}

// Converts given density into pressure
float convert_density_to_pressure(float density)
{
    return (density - rest_density) * stiffness;
}

// Calculate the shared pressure between two densities
float calculate_shared_pressure(float first_density, float second_density)
{
    // Converting both densities to pressure 
    float first_pressure = convert_density_to_pressure(first_density);
    float second_pressure = convert_density_to_pressure(second_density);

    // Computing the pressure that respects Newton's Third Law of Motion (as explained by Sebastian Lague)
    return (first_pressure + second_pressure) / 2;
}

// Given a position in world space, returns its corresponding spatial grid's hash value
int3 position_to_cell_coords(float3 position)
{
    // Computing the coordinates with respect to the kernel radius
    return (int3)floor(position / kernel_radius);
}

// Given two uint3 return 1 if they are identical, 0 otherwise
uint are_int3_equal(int3 first, int3 second)
{
    return all(first == second);
}

// MurmurHash3-Like 3D Hash
uint murmur(uint h)
{
    h ^= h >> 16;
    h *= 0x85ebca6b;
    h ^= h >> 13;
    h *= 0xc2b2ae35;
    h ^= h >> 16;
    return h;
}

// Given a cell position in the spatial grid, returns its corresponding hash value mod particles_amount
uint hash_cell(int3 cell_coords)
{
    uint seed = 42; // You can change the seed
    uint hash = seed;
    hash ^= murmur(cell_coords.x);
    hash ^= murmur(cell_coords.y);
    hash ^= murmur(cell_coords.z);
    return hash % particles_amount;
}

// Given a position in world space, returns the density at the position
float3 calculate_density_at_position(float3 position)
{
    // Computing the coordinates of the grid cell containing the target particle
    int3 target_cell_coords = position_to_cell_coords(position);

    // Initializing the density of the target particle
    float density = 0.0f;
    
    // Neighbours search
    for(int i = 0; i < 27; i++)
    {
        // Computing the current cell coordinates
        int3 current_cell_coords = target_cell_coords + cell_offsets_3D[i];
        
        // Computing the key of the central cell + the current offset
        uint current_cell_key = hash_cell(current_cell_coords);
        
        // Iterating all neighbours within the current cell
        for(uint current_particle_index = spatial_offsets[current_cell_key];
            current_particle_index < particles_amount && spatial_keys[current_particle_index] == current_cell_key;
            current_particle_index++)
        {
            // Storing the predicted position of the current particle
            float3 current_particle_pred_pos = predicted_positions[current_particle_index];
            
            // Verifying that the current particle actually belongs to the current cell
            if(!are_int3_equal(current_cell_coords, position_to_cell_coords(current_particle_pred_pos)))
                continue;
            
            // Computing the vector between the current particle and the target
            float3 current_to_target = current_particle_pred_pos - position;
    
            // Computing the squared distance
            float sqr_distance = dot(current_to_target, current_to_target);
    
            // If the particle is outside kernel radius, continue
            if(sqr_distance >= sqr_kernel_radius)
                continue;
    
            // Adding the current particle influence to the target particle density
            density += particle_mass * poly6(sqr_distance);
        }
    }

    return density;
}

[numthreads(thread_group_size, 1, 1)]
void reorder_buffers(uint target_particle_index : SV_DispatchThreadID)
{
    // Avoid using invalid indices
    if(target_particle_index >= particles_amount)
        return;

    // Mapping sorted indices to unsorted indices
    uint unsorted_index = spatial_indices[target_particle_index];

    // Shifting the data from the main buffers to the auxiliary buffers to for a sorted buffer
    auxiliary_positions[target_particle_index] = positions[unsorted_index];
    auxiliary_predicted_positions[target_particle_index] = predicted_positions[unsorted_index];
    auxiliary_velocities[target_particle_index] = velocities[unsorted_index];
}

[numthreads(thread_group_size, 1, 1)]
void copy_auxiliary_in_main(uint target_particle_index : SV_DispatchThreadID)
{
    // Avoid using invalid indices
    if(target_particle_index >= particles_amount)
        return;

    // Moving the data from the auxiliary buffers to the main buffers
    positions[target_particle_index] = auxiliary_positions[target_particle_index];
    predicted_positions[target_particle_index] = auxiliary_predicted_positions[target_particle_index];
    velocities[target_particle_index] = auxiliary_velocities[target_particle_index];
}

[numthreads(thread_group_size, 1, 1)]
void update_predicted_positions(uint target_particle_index : SV_DispatchThreadID)
{
    // Apply the process only using valid ids
    if(target_particle_index >= particles_amount)
        return;

    // Storing the position and velocity of the target particle
    float3 target_particle_pos = positions[target_particle_index];
    float3 target_particle_vel = velocities[target_particle_index];

    // Computing the predicted position of the target particle
    float3 target_particle_pred_pos = target_particle_pos
        + target_particle_vel * delta_time
        + gravity_acceleration * delta_time;

    // Storing the computed data in the global buffer
    predicted_positions[target_particle_index] = target_particle_pred_pos;
}

[numthreads(thread_group_size, 1, 1)]
void update_spatial_keys(uint target_particle_index : SV_DispatchThreadID)
{
    // Apply the process only using valid ids
    if(target_particle_index >= particles_amount)
        return;

    // Computing the predicted position of the target particle
    float3 target_particle_pred_pos = predicted_positions[target_particle_index];

    // Computing the spatial key of the target particle predicted position
    uint target_particle_spatial_key = hash_cell(position_to_cell_coords(target_particle_pred_pos));

    // Storing the computed data in the global buffer
    spatial_keys[target_particle_index] = target_particle_spatial_key;
}

[numthreads(thread_group_size, 1, 1)]
void update_density(uint target_particle_index : SV_DispatchThreadID)
{
    // Apply the process only using valid ids
    if(target_particle_index >= particles_amount)
        return;
    
    // Updating target particle density
    densities[target_particle_index] = calculate_density_at_position(predicted_positions[target_particle_index]);
}

[numthreads(thread_group_size, 1, 1)]
void update_position_and_velocity(uint target_particle_index : SV_DispatchThreadID)
{
    // Apply the process only using valid ids
    if(target_particle_index >= particles_amount)
        return;
    
    // Initializing the new acceleration of the target particle
    float3 internal_force = 0.0f;

    // Storing the target particles predicted position, density and velocity
    float3 target_particle_pred_pos = predicted_positions[target_particle_index];
    float3 target_particle_velocity = velocities[target_particle_index];
    float target_particle_density = densities[target_particle_index];

    // Computing the coordinates of the grid cell containing the target particle
    int3 target_cell_coords = position_to_cell_coords(target_particle_pred_pos);
    
    // Neighbours search
    for(int i = 0; i < 27; i++)
    {
        // Computing the current cell coordinates
        uint3 current_cell_coords = target_cell_coords + cell_offsets_3D[i];
        
        // Computing the key of the central cell + the current offset
        uint current_cell_key = hash_cell(current_cell_coords);
        
        // Iterating all neighbours within the current cell
        for(uint current_particle_index = spatial_offsets[current_cell_key];
            current_particle_index < particles_amount && spatial_keys[current_particle_index] == current_cell_key;
            current_particle_index++)
        {
            // Skipping calculation for the particle by itself
            if(target_particle_index == current_particle_index)
                continue;
            
            // Storing the predicted position of the current particle
            float3 current_particle_pred_pos = predicted_positions[current_particle_index];
            
            // Verifying that the current particle actually belongs to the current cell
            if(!are_int3_equal(current_cell_coords, position_to_cell_coords(current_particle_pred_pos)))
                continue;

            // Computing the vector between the current particle and the target
            float3 diff_vector = current_particle_pred_pos - target_particle_pred_pos;

            // Computing the squared distance
            float sqr_distance = dot(diff_vector, diff_vector);

            // If the particle is outside kernel radius, continue
            if(sqr_distance >= sqr_kernel_radius || sqr_distance == 0)
                continue;

            // Computing the distance
            float distance = sqrt(sqr_distance);

            // VISCOSITY
        
            // Computing the viscosity's influence of the current particle
            float viscosity_influence = viscosity_laplacian(distance);

            // Computing the difference between velocities
            float3 velocities_difference = velocities[current_particle_index] - target_particle_velocity;

            // Storing the current particle density
            float current_particle_density = densities[current_particle_index];

            // PRESSURE

            // Computing the direction between the current particle and the point of interest
            float3 pressure_direction = distance == 0 ? float3(1, 0, 0) : diff_vector / distance;

            // Evaluating the slope of the kernel at the given distance
            float kernel_slope = spiky_gradient(distance);
        
            // Computing the shared pressure force
            float shared_pressure = calculate_shared_pressure(current_particle_density, target_particle_density);

            // FINAL FORCE

            // Adding the result to the force
            internal_force += (particle_mass * viscosity_influence * viscosity * velocities_difference
                + particle_mass * shared_pressure * kernel_slope * pressure_direction)
                / current_particle_density;
        }
    }

    // Computing the acceleration caused by the density field forces and multiplying it by delta time
    target_particle_velocity += internal_force / target_particle_density * delta_time + gravity_acceleration;
    
    // Updating the position and velocity of the particle
    velocities[target_particle_index] = target_particle_velocity;
    positions[target_particle_index] += target_particle_velocity * delta_time;
    
    // Collision detection
    resolve_collision(target_particle_index);
}

[numthreads(8, 8, 8)]
void update_density_map (uint3 target_density_map_index : SV_DispatchThreadID)
{
    // If the thread is situated outside the density map, skip
    if(target_density_map_index.x >= density_map_size.x ||
        target_density_map_index.y >= density_map_size.y ||
        target_density_map_index.z >= density_map_size.z)
        return;
    
    // Computing the normalized coordinates within the texture
    float3 normalized_texture_pos = target_density_map_index / (density_map_size - 1.0f);

    // Computing the equivalent in simulation local space
    float3 local_pos = (normalized_texture_pos - 0.5) * bounds_size;

    // Converting the position in world space
    float3 world_pos = mul(local_to_world, float4(local_pos, 1.0f)).xyz;

    // Storing the density value
    density_map[target_density_map_index] = calculate_density_at_position(world_pos) / rest_density;
}